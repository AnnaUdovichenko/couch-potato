

buildscript {

    repositories {
        mavenCentral()
        jcenter()
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap" }
        maven{ url "https://kotlin.bintray.com/kotlin-js-wrappers" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:0.12"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.37"
    }
}

apply plugin: "com.moowork.node"
apply plugin: "kotlin2js"
apply plugin: "kotlin-dce-js"

repositories {
    jcenter()
    mavenCentral()
    maven{
        url "https://kotlin.bintray.com/kotlin-js-wrappers"
    }
    maven{ url "https://dl.bintray.com/kotlin/kotlinx"}
}

dependencies {
    def kotlinx_html_version = "0.6.11"
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile "org.jetbrains:kotlin-extensions:1.0.1-pre.49-kotlin-1.2.60"
    compile "org.jetbrains:kotlin-react:16.4.2-pre.49-kotlin-1.2.60"
    compile "org.jetbrains:kotlin-react-dom:16.4.2-pre.49-kotlin-1.2.60"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:${kotlinx_html_version}"
    compile "org.jetbrains:kotlin-styled:1.0.0-pre.55-kotlin-1.2.71"
    testCompile "org.jetbrains.kotlin:kotlin-test-js"
}

compileKotlin2Js {
    kotlinOptions.outputFile = "$project.buildDir.path/js/output.js"
    kotlinOptions.moduleKind = "commonjs"
    kotlinOptions.sourceMap = true
    kotlinOptions.main = "call"
}

node{
    version = "10.9.0"
    npmVersion = "4.0.0"
    download = true
}

task webpack(type: NodeTask, dependsOn: ["npmInstall", "runDceKotlinJs"]) {
    script = file("node_modules/webpack/bin/webpack")
}
processResources.dependsOn "webpack"

task stage(dependsOn: ["build", "clean"])
build.mustRunAfter clean